# Author: Lucas Vilas-Boas
# Year: 2023
# Repo: https://github.com/lucoiso/VulkanRenderer

# ----------- Global Definitions -----------
SET(LIBRARY_NAME RenderCore)

# ------------- Library Setup --------------
SET(PRIVATE_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Modules/Private)
SET(PUBLIC_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Modules/Public)

SET(PRIVATE_MODULES
	${PRIVATE_MODULES_BASE_DIRECTORY}/Window.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/EngineCore.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/ShaderManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/CommandsManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/PipelineManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/BufferManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/DeviceManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/ImGuiManagement.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/Helpers.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/DebugHelpers.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/GLFWCallbacks.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/Camera.cppm
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/RenderUtils.cppm

	${PRIVATE_MODULES_BASE_DIRECTORY}/EngineCore.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/ShaderManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/CommandsManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/PipelineManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/BufferManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/DeviceManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Management/ImGuiManagement.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/Helpers.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/DebugHelpers.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/Constants.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/EnumConverter.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/EnumHelpers.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Utils/GLFWCallbacks.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/DeviceProperties.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/TextureData.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/ObjectData.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/Camera.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/Vertex.ixx
	${PRIVATE_MODULES_BASE_DIRECTORY}/Types/UniformBufferObject.ixx
)

SET(PUBLIC_MODULES
	${PUBLIC_MODULES_BASE_DIRECTORY}/Window.ixx
	${PUBLIC_MODULES_BASE_DIRECTORY}/Utils/RenderUtils.ixx
	${PUBLIC_MODULES_BASE_DIRECTORY}/Types/Object.ixx
	${PUBLIC_MODULES_BASE_DIRECTORY}/Types/Transform.ixx
)

ADD_LIBRARY(${LIBRARY_NAME})
SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES LINKER_LANGUAGE CXX)

TARGET_SOURCES(${LIBRARY_NAME}
			   PRIVATE
			   FILE_SET cxx_private_modules
			   TYPE CXX_MODULES
			   BASE_DIRS ${PRIVATE_MODULES_BASE_DIRECTORY}
			   FILES ${PRIVATE_MODULES}

			   PUBLIC
			   FILE_SET cxx_public_modules
			   TYPE CXX_MODULES
			   BASE_DIRS ${PUBLIC_MODULES_BASE_DIRECTORY}
			   FILES ${PUBLIC_MODULES}
)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE ${PRIVATE_MODULES_BASE_DIRECTORY})
TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE BUILD_DLL)

FIND_PACKAGE(Boost REQUIRED COMPONENTS log)
FIND_PACKAGE(glm REQUIRED)
FIND_PACKAGE(Stb REQUIRED)
FIND_PACKAGE(assimp CONFIG REQUIRED)

FIND_PACKAGE(volk CONFIG REQUIRED)
FIND_PACKAGE(VulkanMemoryAllocator CONFIG REQUIRED)
FIND_PACKAGE(SPIRV-Tools CONFIG REQUIRED)

FIND_PACKAGE(glslang CONFIG REQUIRED)

FIND_PACKAGE(glfw3 CONFIG REQUIRED)
FIND_PACKAGE(imgui CONFIG REQUIRED)

TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PRIVATE
					  Boost::log

					  glm::glm
					  assimp::assimp

					  volk::volk_headers
					  GPUOpen::VulkanMemoryAllocator
					  SPIRV-Tools-static

					  glslang::glslang
					  glslang::glslang-default-resource-limits
					  glslang::SPIRV

					  glfw
					  imgui::imgui

					  Timer
					  Configuration

					  ImGui-Volk-Backend
)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE
						   ${Stb_INCLUDE_DIR}
)

TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE
						   # Assets directory (relative to binaries)
						   DEBUG_SHADER_VERT="Resources/Shaders/TRIANGLE_VERT_SHADER.vert"
						   DEBUG_SHADER_FRAG="Resources/Shaders/TRIANGLE_FRAG_SHADER.frag"
						   DEBUG_MODEL_OBJ="Resources/Assets/VIKING_ROOM_OBJ.obj"
						   DEBUG_MODEL_TEX="Resources/Assets/VIKING_ROOM_TEX.png"
						   EMPTY_TEX="Resources/Assets/EMPTY_TEX.png"
)

TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC
						   GPU_API_DUMP=0
						   VK_NO_PROTOTYPES=1
						   IMGUI_IMPL_VULKAN_NO_PROTOTYPES=1
)

IF (WIN32)
	TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC
							   VK_USE_PLATFORM_WIN32_KHR
							   WIN32_LEAN_AND_MEAN
							   NOMINMAX
							   _WIN32_WINNT=0x0601
	)
ELSEIF (MACOS)
	TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC
							   VK_USE_PLATFORM_MACOS_MVK
	)
ELSEIF (UNIX)
	TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PUBLIC
							   VK_USE_PLATFORM_XCB_KHR
	)
ENDIF (WIN32)
